@module "lexer"

@use "symbols" as sym

pub const create_lexer -> fn (path: *byte) Lexer {
  return Lexer { line: 1, col: 0, src: path, current: path };
}

#returns_ownership
pub const token_value_string -> fn (tok: *Token) *byte {
  let s: *byte = cast<*byte>(alloc((tok.len + 1) * sizeof<byte>));
  loop [i: int = 0](i < tok.len) : (++i) {
    s[i] = tok.val[i];
  }  
  s[tok.len] = '\0';
  return s;
}

const peek -> fn (lx: *Lexer, offset: int) byte { 
  return lx.current[offset]; 
}

const advance -> fn (lx: *Lexer) byte {
  let c: byte = peek(lx, 0);
  lx.current = cast<*byte>(cast<int>(lx.current) + 1);

  if (c == '\n') {
    lx.line = lx.line + 1;
    lx.col = 0;
  } elif (c != '\0') {
    lx.col = lx.col + 1;
  }
  
  return c;
}

const is_end -> fn (lx: *Lexer) int { 
  if (peek(lx, 0) == '\0') return 1;
  return 0;
}

const is_alpha -> fn (c: byte) bool {
  return (c >= 'a' && c <= 'z') || (c >= 'A' && c <= 'Z') || (c == '_');
}
const is_digit -> fn (c: byte) bool { return (c >= '0' && c <= '9'); }
const is_alnum -> fn (c: byte) bool { return is_alpha(c) || is_digit(c); }

const is_whitespace -> fn (c: byte) bool {
  return (c == ' ' || c == '\t' || c == '\n' || c == '\r');
}

const skip_whitespace -> fn (lx: *Lexer) void {
  loop (is_end(lx) == 0 && is_whitespace(peek(lx, 0))) {
    advance(lx);
  }
}

const make_token -> fn (val: *byte, kind: int, len: int) Token {
  return Token { val: val, kind: kind, len: len };
}

pub const next_token -> fn (lx: *Lexer) Token {
  skip_whitespace(lx);

  if (is_end(lx) == 1)
    return make_token(lx.current, sym::TokenKind::EOF, 0);
  
  let start: *byte = lx.current;
  let c: byte = peek(lx, 0);
  
  if (is_digit(c)) {
    let len: int = 0;
    loop (is_end(lx) == 0 && is_digit(peek(lx, 0))) {
      advance(lx);
      len = len + 1;
    }
    return make_token(start, sym::TokenKind::Number, len);
  }

  if (is_alpha(c)) {
    let len: int = 0;
    loop (is_end(lx) == 0 && is_alnum(peek(lx, 0))) {
      advance(lx);
      len = len + 1;
    }
    
    let word: *byte = sym::create_word(start, len);
    defer { free(word); }
    
    let kind: int = sym::lookup_keyword(word);
    if (kind != -1) return make_token(start, kind, len);
    
    return make_token(start, sym::TokenKind::Ident, len);
  }
  
  let kind: int = sym::lookup_single(c);
  if (kind != -1) {
    advance(lx);
    return make_token(start, kind, 1);
  }
  
  advance(lx);
  return next_token(lx);
}

